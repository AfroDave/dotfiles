prompt_custom_human_time() {
    local tmp=$1
    local days=$(( tmp / 60 / 60 / 24 ))
    local hours=$(( tmp / 60 / 60 % 24 ))
    local minutes=$(( tmp / 60 % 60 ))
    local seconds=$(( tmp % 60 ))
    (( $days > 0 )) && echo -n "${days}d "
    (( $hours > 0 )) && echo -n "${hours}h "
    (( $minutes > 0 )) && echo -n "${minutes}m "
    echo "${seconds}s"
}

prompt_custom_git_dirty() {
    # check if we're in a git repo
    command git rev-parse --is-inside-work-tree &>/dev/null || return
    # check if it's dirty
    [[ "$custom_GIT_UNTRACKED_DIRTY" == 0 ]] && local umode="-uno" || local umode="-unormal"
    command test -n "$(git status --porcelain --ignore-submodules ${umode})"

    (($? == 0)) && echo '*'
}

prompt_custom_preexec() {
    cmd_timestamp=$EPOCHSECONDS

    # shows the current dir and executed command in the title when a process is active
    print -Pn "\e]0;"
    echo -nE "$PWD:t: $2"
    print -Pn "\a"
}

prompt_custom_string_length() {
    echo ${#${(S%%)1//(\%([KF1]|)\{*\}|\%[Bbkf])}}
}

prompt_custom_precmd() {
    print -Pn '\e]0;%~\a'

    vcs_info

    local prompt_custom_preprompt="%F{248}%~%F{242}$vcs_info_msg_0_`prompt_custom_git_dirty`%f"
    print -P $prompt_custom_preprompt

    (( ${custom_GIT_PULL:-1} )) && {
        command git rev-parse --is-inside-work-tree &>/dev/null &&
        command git fetch &>/dev/null &&
        command git rev-parse --abbrev-ref @'{u}' &>/dev/null && {
            local arrows=''
            (( $(command git rev-list --right-only --count HEAD...@'{u}' 2>/dev/null) > 0 )) && arrows='⇣'
            (( $(command git rev-list --left-only --count HEAD...@'{u}' 2>/dev/null) > 0 )) && arrows+='⇡'
            print -Pn "\e7\e[A\e[1G\e[`prompt_custom_string_length $prompt_custom_preprompt`C%F{cyan}${arrows}%f\e8"
        }
    } &!

    unset cmd_timestamp
}

prompt_custom_setup() {
    export PROMPT_EOL_MARK=''

    prompt_opts=(cr subst percent)

    zmodload zsh/datetime
    autoload -Uz add-zsh-hook
    autoload -Uz vcs_info

    add-zsh-hook precmd prompt_custom_precmd
    add-zsh-hook preexec prompt_custom_preexec

    zstyle ':vcs_info:*' enable git
    zstyle ':vcs_info:git*' formats ' %b'
    zstyle ':vcs_info:git*' actionformats ' %b|%a'

    [[ "$SSH_CONNECTION" != '' ]] && prompt_custom_username='%n@%m '

    PROMPT='%(?.%F{green}.%F{red})━%f '
    RPROMPT='%F{242}$prompt_custom_username%f'
}

prompt_custom_setup "$@"
